import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    // Increase the timeout when installing the apk
    // Without it, CircleCi gets a com.android.ddmlib.InstallException
    DdmPreferences.setTimeOut(30 * 60 * 1000) // Set the timeout to 30 minutes

    defaultConfig {
        applicationId "com.tytanapps.ptsd"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 34
        versionName "2.0.1"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File propsFile = rootProject.file('app/keystore.properties')
            Properties props = new Properties()
            props.load(new FileInputStream(propsFile))

            storeFile = file(props['storeFile'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
    }
    adbOptions {
        // The default timeout is 5 seconds
        // This is too slow when running on CircleCi, so set a longer timeout
        timeOutInMs 30 * 60 * 1000 // 30 minutes
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:customtabs:25.1.0'

    // The following should all have the same version. Ex. 10.0.1
    compile 'com.google.android.gms:play-services-auth:10.0.1'
    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.google.firebase:firebase-config:10.0.1'
    compile 'com.google.firebase:firebase-database:10.0.1'

    compile 'com.github.Angtrim:Android-Five-Stars-Library:v3.1'
    compile 'com.github.techery.progresshint:library-addition:0.2.3'
    compile 'com.ms-square:expandableTextView:0.1.4'
    compile 'io.doorbell:android-sdk:0.2.6@aar'
    compile 'com.github.marcoscgdev.EasyLicensesDialog:appcompatversion:1.0.4'
    compile 'com.mcxiaoke.volley:library-aar:1.0.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.github.bhargavms:DotLoader:1.0.0'
    compile 'com.squareup.picasso:picasso:2.5.2'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

play {
    jsonFile = file('keys.json')
}

apply plugin: 'com.google.gms.google-services'