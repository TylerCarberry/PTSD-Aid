import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    // Increase the timeout when installing the apk
    // Without it, CircleCi gets a com.android.ddmlib.InstallException
    DdmPreferences.setTimeOut(30 * 60 * 1000) // Set the timeout to 30 minutes

    defaultConfig {
        applicationId "com.tytanapps.ptsd"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 44
        versionName "2.1.1"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            File propsFile = rootProject.file('app/keystore.properties')
            Properties props = new Properties()
            props.load(new FileInputStream(propsFile))

            storeFile = file(props['storeFile'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
    }
    adbOptions {
        // The default timeout is 5 seconds
        // This is too slow when running on CircleCi, so set a longer timeout
        timeOutInMs 30 * 60 * 1000 // 30 minutes
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support libs
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"

    // Google Play Services / Firebase
    compile "com.google.android.gms:play-services-auth:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-analytics:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-core:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-crash:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-messaging:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-config:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-database:$googlePlayServicesVersion"
    compile "com.google.firebase:firebase-perf:$googlePlayServicesVersion"

    // Butterknife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // Dagger
    compile "com.google.dagger:dagger:2.9"
    annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    provided 'javax.annotation:jsr250-api:1.0'

    // Loading animation
    compile "com.github.bhargavms:DotLoader:1.0.0"

    // Picasso
    compile "com.squareup.picasso:picasso:2.5.2"

    // RxAndroid
    compile "io.reactivex:rxandroid:0.25.0"

    compile "com.github.Angtrim:Android-Five-Stars-Library:v3.1"
    compile "com.github.techery.progresshint:library-addition:0.2.3"
    compile "com.ms-square:expandableTextView:0.1.4"
    compile "io.doorbell:android-sdk:0.2.6@aar"
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'se.emilsjolander:StickyScrollViewItems:1.1.0'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.google.code.gson:gson:2.7'

    compile('com.mikepenz:aboutlibraries:5.9.6@aar') {
        transitive = true
    }

    // Leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    testCompile "junit:junit:4.12"

    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
}

play {
    jsonFile = file('keys.json')
}

apply plugin: 'com.google.gms.google-services'